# MDK环境下的代码存储

在完成编译之后，不同的代码变量会被放置在不同的分区，而根据分区的不同将对应的代码放入不同的存储中。在经过编译之后keil会将代码划分四个不同的区域：**Code、RO-data、RW-data、ZI-data**
 - `CODE`：代码区，指程序中代码即函数体的大小，注意程序中**未使用的函数也会算在CODE**中，也即会占用FLASH空间，因此不用的函数最好删除掉，以免占用过多FLASH空间。
 - `RO-DATA`：RO就是只读的意思，程序中**只读的变量**（也就是带Const的）和**已初始化的字符串**等。
 - `RW-DATA`：特指**已初始化**的可读可写全局/静态变量。
 - `ZI-DATA`：**未初始化**的可读可写全局/静态变量，初始化为0也算做未初始化，用到的堆空间和栈空间也会被算入这里面。
局部变量由于只在运行中产生，所以在存储分配的时候无需考虑。存储态和运行态时候变量情况是不一样的。

- **存储态**：在未通电情况下物理内存中只包含`CODE`，`RO-DATA`，`RW-DATA`，也就是在下载到Flash中只存放已经确定的代码和变量，未初始化或者局部变量则不需要考虑。
- **运行态**：当上电后程序运行时，程序会从特定的地址进行启动，启动时会将**RW-data的数据加载到SRAM中**，单片机的`RO-DATA`区域不需要加载到 SRAM，内核直接从 FLASH 读取指令运行。`RW-DATA`+`ZI-DATA`就是程序运行**总共**会占用SRAM的长度，**生成局部变量的栈空间包含在ZI-data区的范围**。

对于flash来说在存储态主要存放`CODE`，`RO-DATA`，`RW-DATA`，SRAM在存储态的时候不存放任何内容。在上电之后，为了更快的运行，会将`RW-DATA`中的内容加载进SRAM中，此外在程序运行中会逐渐解析初始化为0或者未初始化的变量，将其存储在SRAM中，局部变量产生也会存放在SRAM中。

当硬件中存在多个flash或者其他存储介质的时候，MDK通过sct文件完成地址映射和代码指定存储。
# GCC环境下的代码存储

与MDK环境类似GCC也会将代码化为不同的区，不同的是ARMCC编译器会自动完成对应区域的安排，只需要通过 sct文件注册存储和完成地址映射。而GCC则需要通过ld文件完成分区存储和地址映射。这里我们以STM32F429的ld文件为例：
```ls
MEMORY  
{  
  CCMRAM   (xrw)   : ORIGIN = 0x10000000,   LENGTH = 64K  
  RAM      (xrw)   : ORIGIN = 0x20000000,   LENGTH = 192K  
  FLASH    (rx)    : ORIGIN = 0x8000000,    LENGTH = 1024K  
  SDRAM    (rw)    : ORIGIN = 0xC000000,    LENGTH = 32M  
}
```

ld文件主要由两部分组成，`MEMORY`与`SECTIONS`，前者中完成存储的定义，包括了存储的名称、权限、起始地址与大小。其中CCMRAM是F4具有的高速RAM，其能和内核高速通信比普通的RAM快不少。SDRAM则是一块片外的SDRAM32MB大小，CCRAM，RAM，SDRAM则构成了F4的存储器架构。

在SECTIONS则完成代码区域存储的指定：
```ls
SECTIONS  
{  
  /* The startup code into "FLASH" Rom type memory */  
  .isr_vector :  
  {  
    . = ALIGN(4);  
    KEEP(*(.isr_vector)) /* Startup code */  
    . = ALIGN(4);  
  } >FLASH  
  
  /* The program code and other data into "FLASH" Rom type memory */  
  .text :  
  {  
    . = ALIGN(4);  
    *(.text)           /* .text sections (code) */  
    *(.text*)          /* .text* sections (code) */  
    *(.glue_7)         /* glue arm to thumb code */  
    *(.glue_7t)        /* glue thumb to arm code */  
    *(.eh_frame)  
  
    KEEP (*(.init))  
    KEEP (*(.fini))  
  
    . = ALIGN(4);  
    _etext = .;        /* define a global symbols at end of code */  
  } >FLASH  
  
  /* Constant data into "FLASH" Rom type memory */  
  .rodata :  
  {  
    . = ALIGN(4);  
    *(.rodata)         /* .rodata sections (constants, strings, etc.) */  
    *(.rodata*)        /* .rodata* sections (constants, strings, etc.) */  
    . = ALIGN(4);  
  } >FLASH

  /* Initialized data sections into "RAM" Ram type memory */
  .data :  
  {  
    . = ALIGN(4);  
    _sdata = .;        /* create a global symbol at data start */  
    *(.data)           /* .data sections */  
    *(.data*)          /* .data* sections */  
    *(.RamFunc)        /* .RamFunc sections */  
    *(.RamFunc*)       /* .RamFunc* sections */  
  
    . = ALIGN(4);  
    _edata = .;        /* define a global symbol at data end */  
  
  } >RAM AT> FLASH
  /* Uninitialized data section into "RAM" Ram type memory */  
  . = ALIGN(4);  
  .bss :  
  {  
    /* This is used by the startup in order to initialize the .bss section */  
    _sbss = .;         /* define a global symbol at bss start */  
    __bss_start__ = _sbss;  
    *(.bss)  
    *(.bss*)  
    *(COMMON)  
  
    . = ALIGN(4);  
    _ebss = .;         /* define a global symbol at bss end */  
    __bss_end__ = _ebss;  
  } >RAM
...............................................

  .sdram(NOLOAD) :  
  {  
  /*. = 0xC0000000;*/  
    . = ALIGN(32);  
    _slcd = .;  
    *(.lcd)  
    _elcd = .;  
  
  /*. = 0XC01F4000;*/  
    . = ALIGN(32);  
    _ssdram = .;  
    *(.sdram)  
    _esdram = .;  
  
  /*. = 0XC1E30000;*/  
    . = ALIGN(32);  
    _ssdram_map = .;  
    *(.sdram_map)  
    _esdram_map = .;  
  }> SDRAM AT> FLASH
```

在ls脚本中
 - `.`表示当前所在地址；
 - `ALIGN`关键词用于确定对齐的字节；
 - `_sxxx`用于保存section的开始地址；
 - `_exxx`用于保存section的结束地址；
 - 在程序中读取ld的变量使用`extern uint32_t _sxxx`，便可以直接读取相应的地址。
 - `KEEP`保证该段一定在输出文件里，不会被丢弃;
 - `AT`指定段的加载地址,一些初始化的变量需要从flash加载进ram，以加快读取速度

这里列举处主要的分区，还有一些分区稍微指出。我们可以看出主要的分区有text，rodata，data，bss。与MDK中的CODE RO-DATA RW-DATA ZI-DATA一一对应。

- .text(代码段)：用于存放程序执行代码的内存区域，**这部分区域的大小在程序运行前就已经确定，并且内存区域通常属于只读**
- .data(数据段)：通常是用来存放程序中已初始化的全局变量的一块内存区域。一般常量字符串就是放在这里的，程序结束后由系统释放。
- .bss：未初始化的全局变量和static修饰的局部变量(**初始化默认值为0**)。
- .rodata：存放const修饰过的全局变量或被编译器认为是常量的符号，由于常量在编译过程中易被优化为寄存器保存，所以有时候添加常量并不会增加rodata的大小。

# 指定变量地址

对于ARM GCC来说分区与MDK中的分区差不多都是四个主要的区域。但是在GCC与ARMCC / ARMclang中申请内存中各个操作各有不同。

```c
#if defined (__ARMCC_VERSION) && (__ARMCC_VERSION < 6010050)
/* 使用ARMCC AC5编译器 */
static __align(32) uint8_t mem1base[MEM1_MAX_SIZE];        /* 内部SRAM内存池 */  
static __align(32) uint8_t mem2base[MEM2_MAX_SIZE] 
				__attribute__((at(0X10000000)));           /* 内部CCM内存池 */  
static __align(32) uint8_t mem3base[MEM3_MAX_SIZE]
				__attribute__((at(0XC01F4000)));           /* 外部SDRAM内存池 */
#elif defined (__ARMCC_VERSION) && (__ARMCC_VERSION >= 6100050)
/* 使用ARMClang AC6编译器 */
static __ALIGNED(32) uint8_t mem1base[MEM1_MAX_SIZE];      /* 内部SRAM内存池 */  
static __ALIGNED(32) uint8_t mem2base[MEM2_MAX_SIZE] 
	__attribute__((section(".bss.ARM.__at_0X10000000")));  /* 内部CCM内存池 */  
static __ALIGNED(32) uint8_t mem3base[MEM3_MAX_SIZE] 
	__attribute__((section(".bss.ARM.__at_0XC01F4000")));  /* 外部SDRAM内存池 */
#elif defined ( __GNUC__ )
/* 使用GCC编译器 */
static uint8_t mem1base[MEM1_MAX_SIZE] ;                   /* 内部SRAM内存池 */  
static uint8_t mem2base[MEM2_MAX_SIZE] 
				__attribute__((section(".ccm")));          /* 内部CCM内存池 */  
static uint8_t mem3base[MEM3_MAX_SIZE] 
				__attribute__((section(".sdram")));        /* 外部SDRAM内存池 */
```

使用ARMCC/ARMClang的时候只需要指定变量的地址便可以，需要注意如果是片外存储器先通过sct文件完成存储器定义与地址映射。当使用GCC编译器的时候需要现在ld文件中设置特定的section，通过section完成变量的寻址存放。这两种方式各有优劣，前者需要考略同一区域变量存放时候越界，而后者则指定精确地址比较困难。且ld的语法也需要学习。